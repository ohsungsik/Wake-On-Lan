@echo off
@chcp 65001 > nul
@setlocal EnableDelayedExpansion

@REM Visual Studio ì„¤ì¹˜ ê²½ë¡œ ì°¾ê¸°
for /f "usebackq tokens=1* delims=: " %%i in (`"%ProgramFiles(x86)%\Microsoft Visual Studio\Installer\vswhere.exe" -latest -requires Microsoft.Component.MSBuild`) do (
    if /i "%%i"=="installationPath" set VS_PATH=%%j
)

if not defined VS_PATH (
    echo Error: Visual Studio installation not found.
    echo Please ensure Visual Studio with C++ workload is installed.
    pause
    exit /b 1
)

@REM Visual Studio í™˜ê²½ ë³€ìˆ˜ ì„¤ì •
set "VSDEVCMD_PATH=%VS_PATH%\Common7\Tools\VsDevCmd.bat"
if not exist "%VSDEVCMD_PATH%" (
    echo Error: VsDevCmd.bat not found at expected location: %VSDEVCMD_PATH%
    pause
    exit /b 1
)

@REM Visual Studio ê°œë°œ í™˜ê²½ ì´ˆê¸°í™”
call "%VSDEVCMD_PATH%" > nul
if %errorlevel% neq 0 (
    echo Error: Failed to initialize VS development environment
    pause
    exit /b 1
)

@REM ë¹Œë“œ ëŒ€ìƒ êµ¬ì„±
set "CONFIGS=Debug Release"
set "PLATFORMS=x64 x86 ARM64"
set "HAS_FAILED=0"
set "BUILD_COUNT=0"

@REM ê²°ê³¼ ì €ì¥ì„ ìœ„í•œ ë°°ì—´ ì‚¬ìš©
set "RESULT_PREFIX=BUILD_RESULT_"

@REM Color codes
set "COLOR_GREEN=[32m"
set "COLOR_RED=[31m"
set "COLOR_RESET=[0m"

@REM ëª¨ë“  .sln íŒŒì¼ì— ëŒ€í•´ì„œ ë¹Œë“œ
for %%S in (*.sln) do (
    echo ===================================================
    echo Building solution: %%S
    echo ===================================================
    
    for %%C in (%CONFIGS%) do (
        for %%P in (%PLATFORMS%) do (
            echo [INFO] Building %%C^|%%P for solution %%S ...
            
            @REM MSBuild ëª…ë ¹ ì‹¤í–‰
            call MSBuild "%%S" /p:Configuration=%%C /p:Platform=%%P /t:Rebuild /m
            
            if errorlevel 1 (
                echo [ERROR] Build failed: %%S [%%C^|%%P]
                set "%RESULT_PREFIX%!BUILD_COUNT!=!COLOR_RED!Failed    | %%~nS [%%C | %%P]!COLOR_RESET!"
                set "HAS_FAILED=1"
            ) else (
                echo [SUCCESS] Build succeeded: %%S [%%C^|%%P]
                set "%RESULT_PREFIX%!BUILD_COUNT!=!COLOR_GREEN!Succeeded | %%~nS [%%C | %%P]!COLOR_RESET!"
            )
            set /a "BUILD_COUNT+=1"
            echo.
        )
    )
)

@REM ë¹Œë“œ ê²°ê³¼ ìš”ì•½ ì¶œë ¥
echo ==================================================
echo Build Summary:
echo.
echo   Status  ^| Project
echo ----------^|--------------------------------------

@REM ê²°ê³¼ ë°°ì—´ ì¶œë ¥
for /l %%i in (0,1,!BUILD_COUNT!) do (
    if defined %RESULT_PREFIX%%%i (
        echo !%RESULT_PREFIX%%%i!
    )
)

echo ==================================================

@REM ì‹¤íŒ¨ ì—¬ë¶€ ê²€ì‚¬
if %HAS_FAILED% EQU 1 (
    echo !COLOR_RED![SUMMARY] ì¼ë¶€ ë¹Œë“œê°€ ì‹¤íŒ¨í–ˆìŠµë‹ˆë‹¤.!COLOR_RESET!
    pause
    exit /b 1
) else (
    echo !COLOR_GREEN![SUMMARY] ëª¨ë“  ë¹Œë“œê°€ ì„±ê³µì ìœ¼ë¡œ ì™„ë£Œë˜ì—ˆìŠµë‹ˆë‹¤.!COLOR_RESET!
    pause
    exit /b 0
)